#!/usr/bin/perl

=head1 NAME

dh_installpam - install pam support files

=cut

use strict;
use warnings;
use Debian::Debhelper::Dh_Lib;

our $VERSION = DH_BUILTIN_VERSION;

=head1 SYNOPSIS

B<dh_installpam> [S<I<debhelper options>>] [B<--name=>I<name>]

=head1 DESCRIPTION

B<dh_installpam> is a debhelper program that is responsible for installing
files used by PAM into package build directories.

=head1 FILES

=over 4

=item debian/I<package>.pam

Installed into usr/lib/pam.d/I<package> in the package build directory.

Until compatibility level 14 this file was installed under
etc/pam.d/I<package>. Please consider using the "rm_conffile" feature from
L<dh_installdeb(1)> to ensure the proper removal of previous PAM files.

=back

=head1 OPTIONS

=over 4

=item B<--name=>I<name>

Look for files named debian/I<package>.I<name>.pam and install them as
usr/lib/pam.d/I<name>, instead of using the usual files and installing them
using the package name.

=back

=cut

init();

# PROMISE: DH NOOP WITHOUT pam cli-options()

foreach my $package (@{$dh{DOPACKAGES}}) {
	my $tmp=tmpdir($package);
	my $pam=pkgfile($package,"pam");

	my $pamd_dir="/usr/lib/pam.d";
	if (compat(13)) {
		$pamd_dir="/etc/pam.d";
	}

	if ($pam ne '') {
		install_dir("$tmp/$pamd_dir");
		install_file($pam,"$tmp/$pamd_dir/".pkgfilename($package));
	}
}

=head1 SEE ALSO

L<debhelper(7)>

This program is a part of debhelper.

=head1 AUTHOR

Joey Hess <joeyh@debian.org>

=cut
